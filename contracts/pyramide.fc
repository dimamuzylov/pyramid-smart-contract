#include "imports/stdlib.fc";
#include "imports/methods.fc";
#include "imports/functions.fc";
#include "imports/const.fc";
#include "imports/storage.fc";
#include "admin.fc";
#include "user.fc";

() recv_internal(int my_balance, int msg_value, cell in_msg_full, slice in_msg_body) impure {
    if (in_msg_body.slice_empty?()) { ;; ignore all empty messages
        return ();
    }


    slice cs = in_msg_full.begin_parse();
    int flags = cs~load_uint(4);

    if (flags & 1) { ;; ignore all bounced messages
        return ();
    }

    slice sender_adress = cs~load_msg_addr();

    load_data();


    int op = in_msg_body~load_uint(32);
    int is_admin = equal_slices(sender_adress, db::admin_addr);

    if (op == op::user_deposit()) {
        int days = in_msg_body~load_uint(32);
        slice ref_address = in_msg_body~load_msg_addr();
        user::deposit(sender_adress, msg_value, days, ref_address);
        pack_data();
        return ();
    }

    if (op == op::user_withdraw()) {
        user::withdraw(sender_adress);
        pack_data();
        return ();
    }

    if (op == op::withdraw()) {
        throw_if(0xfffd, is_admin == 0);
        int amount = in_msg_body~load_coins();
        adm::withdraw(amount);
        return ();
    }

    if (op == op::reset()) {
        throw_if(0xfffd, is_admin == 0);
        reset_data();
        return ();
    }

    throw(0xffff); ;; if the message contains an op that is not known to this contract, we throw
}

tuple get_users() method_id {
    load_data();

    tuple list = null();

    cell d = db::users;
    do {
        (d, slice key, slice user, int found) = d.dict_delete_get_min(ADDRESS_SIZE);
        if (found) {
            (int wc, int address_hash) = unpack_address(key);
            tuple t = empty_tuple();
            t~tpush(pair(wc, address_hash)); ;; tuple address together
            t~tpush(user~load_uint(64)); ;; unlock date
            t~tpush(user~load_coins()); ;; deposit amount
            t~tpush(user~load_uint(32)); ;; deposit days
            t~tpush(user~load_uint(32)); ;; referrals count
            t~tpush(user~load_msg_addr()); ;; referral address
            list = cons(t,list);
        }
    } until (~ found);

    return list;
}

tuple get_user(slice address) method_id {
    load_data();

    if(_is_user_exist(address) == 0) {
        return null();
    }

    slice user = _get_user(address);

    tuple t = empty_tuple();
    t~tpush(user~load_uint(64)); ;; unlock date
    t~tpush(user~load_coins()); ;; deposit amount
    t~tpush(user~load_uint(32)); ;; deposit days
    t~tpush(user~load_uint(32)); ;; referrals count
    t~tpush(user~load_msg_addr()); ;; referral address

    return t;
}

int get_contract_balance() method_id {
    var [balance, _] = get_balance();
    return balance;
}